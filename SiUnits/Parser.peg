@namespace SiUnits
@classname Parser<T>
@using System.Linq

start <Factor<T>> -export -public = _ f:factor _ EOF { f }

factor <Factor<T>> -memoize
    = left:factor (_ [*·⋅] _ / WS) right:power { left * right }
    / left:factor _ "/" _ right:power { left / right }
    / power

power <Factor<T>>
    = left:primary _ "^" _ right:int { left.Pow(right) }
    / primary

primary <Factor<T>> -memoize
    = numberFactor
    / siUnitFactor
    / nameFactor
    / "(" f:factor ")" { f }

numberFactor <NumberFactor<T>>
    = n:int { new NumberFactor<T>(T.CreateChecked(n)) }

siUnitFactor <Factor<T>>
    = p:siPrefixSymbol n:siUnitSymbol !name { p * n }
    / n:siUnitSymbol !name { n }
    / p:siPrefix n:siUnitName !name { p * n }
    / n:siUnitName !name { n }

siPrefixSymbol <NumberFactor<T>>
    = "da" { Units<T>.Deca }
    / "h"  { Units<T>.Hecto }
    / "k"  { Units<T>.Kilo }
    / "M"  { Units<T>.Mega }
    / "G"  { Units<T>.Giga }
    / "T"  { Units<T>.Tera }
    / "P"  { Units<T>.Peta }
    / "E"  { Units<T>.Exa }
    / "Z"  { Units<T>.Zetta }
    / "Y"  { Units<T>.Yotta }
    / "R"  { Units<T>.Ronna }
    / "Q"  { Units<T>.Quetta }
    / "d"  { Units<T>.Deci }
    / "c"  { Units<T>.Centi }
    / "m"  { Units<T>.Milli }
    / "μ"  { Units<T>.Micro }
    / "n"  { Units<T>.Nano }
    / "p"  { Units<T>.Pico }
    / "f"  { Units<T>.Femto }
    / "a"  { Units<T>.Atto }
    / "z"  { Units<T>.Zepto }
    / "y"  { Units<T>.Yocto }
    / "r"  { Units<T>.Ronto }
    / "q"  { Units<T>.Quecto }

siPrefix <NumberFactor<T>>
    = "deca"i   { Units<T>.Deca }
    / "hecto"i  { Units<T>.Hecto }
    / "kilo"i   { Units<T>.Kilo }
    / "mega"i   { Units<T>.Mega }
    / "giga"i   { Units<T>.Giga }
    / "tera"i   { Units<T>.Tera }
    / "peta"i   { Units<T>.Peta }
    / "exa"i    { Units<T>.Exa }
    / "zetta"i  { Units<T>.Zetta }
    / "yotta"i  { Units<T>.Yotta }
    / "ronna"i  { Units<T>.Ronna }
    / "quetta"i { Units<T>.Quetta }
    / "deci"i   { Units<T>.Deci }
    / "centi"i  { Units<T>.Centi }
    / "milli"i  { Units<T>.Milli }
    / "micro"i  { Units<T>.Micro }
    / "nano"i   { Units<T>.Nano }
    / "pico"i   { Units<T>.Pico }
    / "femto"i  { Units<T>.Femto }
    / "atto"i   { Units<T>.Atto }
    / "zepto"i  { Units<T>.Zepto }
    / "yocto"i  { Units<T>.Yocto }
    / "ronto"i  { Units<T>.Ronto }
    / "quecto"i { Units<T>.Quecto }

siUnitSymbol <Factor<T>>
    = "m"   { Units<T>.Distance.Meter }
    / "g"   { Units<T>.Mass.Gram }
    / "s"   { Units<T>.Time.Second }
    / "A"   { Units<T>.ElectricCurrent.Ampere }
    / "K"   { Units<T>.Temperature.Kelvin }
    / "mol" { Units<T>.Quantity.Mole }
    / "cd"  { Units<T>.LuminousIntensity.Candela }
    / "Hz"  { Units<T>.Frequency.Hertz }
    / "N"   { Units<T>.Force.Newton }
    / "Pa"  { Units<T>.Pressure.Pascal }
    / "J"   { Units<T>.Energy.Joule }
    / "W"   { Units<T>.Power.Watt }
    / "C"   { Units<T>.ElectricCharge.Coulomb }
    / "V"   { Units<T>.ElectricPotential.Volt }
    / "F"   { Units<T>.ElectricalCapacitance.Farad }
    / "Ω"   { Units<T>.ElectricalResistance.Ohm }
    / "S"   { Units<T>.ElectricConductance.Siemens }
    / "Wb"  { Units<T>.MagneticFlux.Weber }
    / "T"   { Units<T>.MagneticInductance.Tesla }
    / "H"   { Units<T>.ElectricalInductance.Henry }

siUnitName <Factor<T>>
    = "meter"i "s"i?   { Units<T>.Distance.Meter }
    / "metre"i "s"i?   { Units<T>.Distance.Meter }
    / "gram"i "s"i?    { Units<T>.Mass.Gram }
    / "second"i "s"i?  { Units<T>.Time.Second }
    / "ampere"i "s"i?  { Units<T>.ElectricCurrent.Ampere }
    / "kelvin"i        { Units<T>.Temperature.Kelvin }
    / "mole"i "s"i?    { Units<T>.Quantity.Mole }
    / "candela"i "s"i? { Units<T>.LuminousIntensity.Candela }
    / "hertz"i         { Units<T>.Frequency.Hertz }
    / "newton"i "s"i?  { Units<T>.Force.Newton }
    / "pascal"i "s"i?  { Units<T>.Pressure.Pascal }
    / "joule"i "s"i?   { Units<T>.Energy.Joule }
    / "watt"i "s"i?    { Units<T>.Power.Watt }
    / "coulomb"i "s"i? { Units<T>.ElectricCharge.Coulomb }
    / "volt"i "s"i?    { Units<T>.ElectricPotential.Volt }
    / "farad"i "s"i?   { Units<T>.ElectricalCapacitance.Farad }
    / "ohm"i "s"i?     { Units<T>.ElectricalResistance.Ohm }
    / "siemens"i       { Units<T>.ElectricConductance.Siemens }
    / "weber"i "s"i?   { Units<T>.MagneticFlux.Weber }
    / "tesla"i "s"i?   { Units<T>.MagneticInductance.Tesla }
    / "henry"i         { Units<T>.ElectricalInductance.Henry }
    / "henries"i       { Units<T>.ElectricalInductance.Henry }

nameFactor <NameFactor<T>>
    = n:name { new NameFactor<T>(n) }

name
    = "" (c:. &{ char.IsLetter(c[0]) })+

int <int>
    = negative:[-−]? digits:("" [0-9]+) { negative.Count == 0 ? int.Parse(digits) : -int.Parse(digits) }

_   = "" WS?
WS  = "" [ \t\v\f\u00A0\uFEFF\u1680\u180E\u2000-\u200A\u202F\u205F\u3000]+
EOF = !.
