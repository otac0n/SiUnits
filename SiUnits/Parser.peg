@namespace SiUnits
@classname Parser<T>
@using System.Linq

start <Factor<T>> = _ f:factor _ EOF { f }

factor <Factor<T>> -memoize
    = left:factor (_ [*·⋅] _ / WS) right:power { left * right }
    / left:factor _ "/" _ right:power { left / right }
    / power

power <Factor<T>>
    = left:primary _ "^" _ right:int { left.Pow(right) }
    / primary

primary <Factor<T>> -memoize
    = numberFactor
    / siUnitFactor
    / nameFactor
    / "(" f:factor ")" { f }

numberFactor <NumberFactor<T>>
    = n:int { new NumberFactor<T>(T.CreateChecked(n)) }

siUnitFactor <Factor<T>>
    = p:siPrefixSymbol n:siUnitSymbol !name { p * n }
    / n:siUnitSymbol !name { n }
    / p:siPrefix n:siUnitName !name { p * n }
    / n:siUnitName !name { n }

siPrefixSymbol <NumberFactor<T>>
    = "da" { Factors<T>.Deca }
    / "h"  { Factors<T>.Hecto }
    / "k"  { Factors<T>.Kilo }
    / "M"  { Factors<T>.Mega }
    / "G"  { Factors<T>.Giga }
    / "T"  { Factors<T>.Tera }
    / "P"  { Factors<T>.Peta }
    / "E"  { Factors<T>.Exa }
    / "Z"  { Factors<T>.Zetta }
    / "Y"  { Factors<T>.Yotta }
    / "R"  { Factors<T>.Ronna }
    / "Q"  { Factors<T>.Quetta }
    / "d"  { Factors<T>.Deci }
    / "c"  { Factors<T>.Centi }
    / "m"  { Factors<T>.Milli }
    / "μ"  { Factors<T>.Micro }
    / "n"  { Factors<T>.Nano }
    / "p"  { Factors<T>.Pico }
    / "f"  { Factors<T>.Femto }
    / "a"  { Factors<T>.Atto }
    / "z"  { Factors<T>.Zepto }
    / "y"  { Factors<T>.Yocto }
    / "r"  { Factors<T>.Ronto }
    / "q"  { Factors<T>.Quecto }

siPrefix <NumberFactor<T>>
    = "deca"i   { Factors<T>.Deca }
    / "hecto"i  { Factors<T>.Hecto }
    / "kilo"i   { Factors<T>.Kilo }
    / "mega"i   { Factors<T>.Mega }
    / "giga"i   { Factors<T>.Giga }
    / "tera"i   { Factors<T>.Tera }
    / "peta"i   { Factors<T>.Peta }
    / "exa"i    { Factors<T>.Exa }
    / "zetta"i  { Factors<T>.Zetta }
    / "yotta"i  { Factors<T>.Yotta }
    / "ronna"i  { Factors<T>.Ronna }
    / "quetta"i { Factors<T>.Quetta }
    / "deci"i   { Factors<T>.Deci }
    / "centi"i  { Factors<T>.Centi }
    / "milli"i  { Factors<T>.Milli }
    / "micro"i  { Factors<T>.Micro }
    / "nano"i   { Factors<T>.Nano }
    / "pico"i   { Factors<T>.Pico }
    / "femto"i  { Factors<T>.Femto }
    / "atto"i   { Factors<T>.Atto }
    / "zepto"i  { Factors<T>.Zepto }
    / "yocto"i  { Factors<T>.Yocto }
    / "ronto"i  { Factors<T>.Ronto }
    / "quecto"i { Factors<T>.Quecto }

siUnitSymbol <Factor<T>>
    = "m"   { Factors<T>.Meter }
    / "g"   { Factors<T>.Gram }
    / "s"   { Factors<T>.Second }
    / "A"   { Factors<T>.Ampere }
    / "K"   { Factors<T>.Kelvin }
    / "mol" { Factors<T>.Mole }
    / "cd"  { Factors<T>.Candela }
    / "Hz"  { Factors<T>.Hertz }
    / "N"   { Factors<T>.Newton }
    / "Pa"  { Factors<T>.Pascal }
    / "J"   { Factors<T>.Joule }
    / "W"   { Factors<T>.Watt }
    / "C"   { Factors<T>.Coulomb }
    / "V"   { Factors<T>.Volt }
    / "F"   { Factors<T>.Farad }
    / "Ω"   { Factors<T>.Ohm }
    / "S"   { Factors<T>.Siemens }
    / "Wb"  { Factors<T>.Weber }
    / "T"   { Factors<T>.Tesla }
    / "H"   { Factors<T>.Henry }

siUnitName <Factor<T>>
    = "meter"i "s"i?   { Factors<T>.Meter }
    / "metre"i "s"i?   { Factors<T>.Meter }
    / "gram"i "s"i?    { Factors<T>.Gram }
    / "second"i "s"i?  { Factors<T>.Second }
    / "ampere"i "s"i?  { Factors<T>.Ampere }
    / "kelvin"i        { Factors<T>.Kelvin }
    / "mole"i "s"i?    { Factors<T>.Mole }
    / "candela"i "s"i? { Factors<T>.Candela }
    / "hertz"i         { Factors<T>.Hertz }
    / "newton"i "s"i?  { Factors<T>.Newton }
    / "pascal"i "s"i?  { Factors<T>.Pascal }
    / "joule"i "s"i?   { Factors<T>.Joule }
    / "watt"i "s"i?    { Factors<T>.Watt }
    / "coulomb"i "s"i? { Factors<T>.Coulomb }
    / "volt"i "s"i?    { Factors<T>.Volt }
    / "farad"i "s"i?   { Factors<T>.Farad }
    / "ohm"i "s"i?     { Factors<T>.Ohm }
    / "siemens"i       { Factors<T>.Siemens }
    / "weber"i "s"i?   { Factors<T>.Weber }
    / "tesla"i "s"i?   { Factors<T>.Tesla }
    / "henry"i         { Factors<T>.Henry }
    / "henries"i       { Factors<T>.Henry }

nameFactor <NameFactor<T>>
    = n:name { new NameFactor<T>(n) }

name
    = "" (c:. &{ char.IsLetter(c[0]) })+

int <int>
    = negative:[-−]? digits:("" [0-9]+) { negative.Count == 0 ? int.Parse(digits) : -int.Parse(digits) }

_   = "" WS?
WS  = "" [ \t\v\f\u00A0\uFEFF\u1680\u180E\u2000-\u200A\u202F\u205F\u3000]+
EOF = !.
